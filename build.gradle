import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'
    id 'checkstyle'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

defaultTasks 'clean', 'updateLicenses', 'build', 'shadowJar'

group = 'net.caseif.flint.steel'
version = '1.3.8-SNAPSHOT'

ext.projectName = 'steel'
ext.description = 'The implementation of Flint minigame framework for the Bukkit Minecraft server mod.'
ext.inceptionYear = '2015'
ext.packaging = 'jar'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

configurations {
    shadow
    compile {
        extendsFrom shadow
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'caseif'
        url 'https://repo.caseif.net/'
    }
    maven {
        name = 'spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

ext {
    author = 'Max Roncace'

    versionSuffix = version.contains('SNAPSHOT') ? (
            (
                    System.getenv('GIT_COMMIT') ? ('-git('
                            + System.getenv('GIT_BRANCH')
                            .substring(System.getenv('GIT_BRANCH').split('/')[0].length() + 1) + '-'
                            + System.getenv('GIT_COMMIT').substring(0, 7) + ')') : ''
            )
                    + (System.getenv('BUILD_NUMBER') ? '-jnks' + System.getenv('BUILD_NUMBER') : '')
    ) : ''

    commonVersion = '1.3.6'

    guava = 'com.google.guava:guava:17.0'
    bukkit = 'org.spigotmc:spigot-api:1.14-pre5-SNAPSHOT'
    common = 'net.caseif.flint.common:flintcommon:' + commonVersion
    bstats = 'org.bstats:bstats-bukkit:1.2'
    jtelemetry = 'net.caseif.jtelemetry:jtelemetry:1.1.0'
    gson = 'com.google.code.gson:gson:2.2.4'
    json_simple = 'com.googlecode.json-simple:json-simple:1.1.1'
}

dependencies {
    implementation guava
    implementation bukkit
    shadow common
    shadow bstats
    shadow jtelemetry
    shadow gson
    shadow json_simple

    checkstyle 'org.spongepowered:checkstyle:6.1.1-sponge1'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += '-Xlint:all'
}

jar {
    classifier = 'base'
}

jar.manifest.mainAttributes(
        'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        'Specification-Title': 'Flint',
        'Specification-Version': '1.3.1',
        'Specification-Vendor': 'Max Roncace',
        'Implementation-Title': name,
        'Implementation-Version': version + versionSuffix,
        'Implementation-Vendor': author
)

processResources {
    from 'LICENSE'
    filter { String line -> line.replace('SNAPSHOT', 'SNAPSHOT' + versionSuffix) }
    filter ReplaceTokens, tokens: [
            "name": project.name,
            "version": project.version
    ]
}

license {
    include '**/*.java'
    ignoreFailures false
}

checkstyle {
    configProperties = [
            'name': project.name,
            'year': project.inceptionYear,
            'basedir': project.projectDir,
            'severity': 'warning'
    ]
    configFile = file('etc/checkstyle.xml')
}

tasks.withType(Checkstyle) {
    exclude '**/*.properties'
    exclude '**/*.yml'
}

shadowJar {
    configurations = [project.configurations.shadow]

    dependencies {
        exclude dependency('com.google.guava:guava')
        exclude dependency('org.bukkit:bukkit')
        exclude dependency('junit:junit')
    }

    classifier = ''

    relocate('org.bstats', 'net.caseif.flint.steel.lib.org.bstats')
    relocate('net.caseif.jtelemetry', 'net.caseif.flint.steel.lib.net.caseif.jtelemetry')
    relocate('com.google.gson', 'net.caseif.flint.steel.lib.com.google.gson')
    relocate('org.json.simple', 'net.caseif.flint.steel.lib.org.json.simple')
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

artifacts {
    archives shadowJar
    archives sourceJar
}

wrapper {
    gradleVersion = '7.4'
}
